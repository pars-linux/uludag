#!/usr/bin/python

import os
import sys
import glob
import shutil
from distutils.sysconfig import get_config_var as pyconf

import SCons.Warnings
SCons.Warnings.warningAsException(1)

# Parameters

version = ARGUMENTS.get('version', '1.4')
prefix = ARGUMENTS.get('prefix', 'install')
pyprefix = ARGUMENTS.get('pythondir', 'auto')

# Source Distribution

distfiles = """
    SConstruct
    README
    etc/*.xml
    src/*.c
    src/SConscript
    include/*.h
    bindings/SConscript
    bindings/*.c
    bindings/*.h
    csapi/SConscript
    csapi/*.c
    po/comar.pot
    po/*.po
"""

if 'dist' in sys.argv:
    distdir = "comar-%s" % version
    list = []
    for t in Split(distfiles):
        list.extend(glob.glob(t))
    if os.path.exists(distdir):
        shutil.rmtree(distdir)
    os.mkdir(distdir)
    for file_ in list:
        cum = distdir[:]
        for d in os.path.dirname(file_).split('/'):
            dn = os.path.join(cum, d)
            cum = dn[:]
            if not os.path.exists(dn):
                os.mkdir(dn)
        shutil.copy(file_, os.path.join(distdir, file_))
    os.popen("tar -czf %s %s" % ("comar-" + version + ".tar.gz", distdir))
    shutil.rmtree(distdir)
    Exit(0)

# i18n

i18n_source_list = [ "src/main.c", "src/cfg.c" ]

if "update_messages" in sys.argv:
    os.system("xgettext --default-domain=comar --keyword=_ --keyword=N_ -o po/comar.pot %s"
        % " ".join(i18n_source_list))
    for item in os.listdir("po"):
        if item.endswith(".po"):
            os.system("msgmerge -q -o temp.po po/%s po/comar.pot" % item)
            os.system("cp temp.po po/%s" % item)
    os.system("rm -f temp.po")
    Exit(0)

# Configuration

if os.path.exists("/etc/pardus-release"):
    # Jolly good, we are on a Pardus system
    # Pisi and Comar python modules goes to the /usr/lib/pardus for reliability
    # Python is patched to import from this directory first
    pyprefix = 'usr/lib/pardus'

if sys.version_info[1] == 3:
    libs = [ 'db', 'm', "python2.3" ]
    if pyprefix == 'auto':
        pyprefix = 'usr/lib/python2.3/site-packages'
else:
    libs = [ 'db', 'm', "python2.4" ]
    if pyprefix == 'auto':
        pyprefix = 'usr/lib/python2.4/site-packages'

libpath = [ '/usr/lib' ]
cpppath = [ pyconf('CONFINCLUDEPY'), "../include" ]
ccflags = "-O2 -Wall -DVERSION='\"%s\"' " % version

env = Environment(CCFLAGS=ccflags, CPPPATH=cpppath, LIBS=libs, LIBPATH=libpath)

# Build

env.SConsignFile("sconsign")
Export('env')
comar = SConscript('src/SConscript', build_dir='build', duplicate=0)
libcomar, hav = SConscript('bindings/SConscript', build_dir='build/bindings', duplicate=0)
csapi = SConscript('csapi/SConscript', build_dir='build/csapi', duplicate=0)


# Install

bindir = os.path.join(prefix, 'usr/bin')
libdir = os.path.join(prefix, 'usr/lib')
incdir = os.path.join(prefix, 'usr/include')
etcdir = os.path.join(prefix, 'etc/comar')
pydir = os.path.join(prefix, pyprefix)

if "install" in sys.argv:
    for item in os.listdir("po"):
        if item.endswith(".po"):
            lang = item[:-3]
            dest = "usr/share/locale/%s/LC_MESSAGES/comar.mo" % lang
            try:
                os.makedirs(os.path.dirname(os.path.join(prefix, dest)))
            except:
                pass
            os.system("msgfmt po/%s -o %s" % (item, os.path.join(prefix, dest)))

targets = []
targets.append(env.Install(bindir, comar))
targets.append(env.Install(etcdir, 'etc/model.xml'))
targets.append(env.Install(etcdir, 'etc/security-policy.xml'))
targets.append(env.Install(etcdir, 'etc/security-comments.xml'))
targets.append(env.Install(incdir, 'bindings/comar.h'))
targets.append(env.Install(libdir, libcomar))
targets.append(env.Install(pydir, csapi))
targets.append(env.Install(bindir, hav))

env.Alias('install', targets)
