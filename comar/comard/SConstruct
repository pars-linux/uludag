#!/usr/bin/python

import os
import sys
from distutils.sysconfig import get_config_var as pyconf

# Parameters

version = ARGUMENTS.get('version', '1.0')
prefix = ARGUMENTS.get('prefix', 'install')

# Source Distribution

distfiles = """
    SConstruct
    README
    model.xml
    src/*.c
    src/SConscript
    include/*.h
    bindings/SConscript
    bindings/comar.py
    bindings/*.c
    bindings/*.h
"""

if 'dist' in sys.argv:
    os.popen("tar -czf %s %s" % ("comar-" + version + ".tar.gz", " ".join(Split(distfiles))))
    Exit(0)

# Configuration

if sys.version_info[1] == 3:
    libs = [ 'db', 'm', "python2.3" ]
else:
    libs = [ 'db', 'm', "python2.4" ]

libpath = [ '/usr/lib' ]
cpppath = [ pyconf('CONFINCLUDEPY'), "../include" ]
ccflags = "-O2 -Wall -DVERSION='\"%s\"' " % version

env = Environment(CCFLAGS=ccflags, CPPPATH=cpppath, LIBS=libs, LIBPATH=libpath)

# Build

Export('env')
comar = SConscript('src/SConscript', build_dir='build', duplicate=0)
libcomar, hav = SConscript('bindings/SConscript', build_dir='build/bindings', duplicate=0)

# Install

bindir = os.path.join(prefix, 'usr/bin')
libdir = os.path.join(prefix, 'usr/lib')
incdir = os.path.join(prefix, 'usr/include')
etcdir = os.path.join(prefix, 'etc/comar')

env.Install(bindir, comar)
env.Install(etcdir, 'model.xml')
env.Install(incdir, 'bindings/libcomar.h')
env.Install(libdir, libcomar)
env.Install(bindir, hav)

env.Alias('install', [ bindir, etcdir, libdir, incdir ])
